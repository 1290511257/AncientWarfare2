import org.apache.tools.ant.filters.ReplaceTokens

ext.versionFile = file('gradle.properties')

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

// We apply the 'forge' plugin, so ForgeGradle can do it's stuff.
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    ivy {
        name "FTBUtilities"
        artifactPattern "http://addons-origin.cursecdn.com/files/${ftbutilities_cf}/[module]-[revision].[ext]"
    }
    ivy {
        name "FTBLib"
        artifactPattern "http://addons-origin.cursecdn.com/files/${ftblib_cf}/[module]-[revision].[ext]"
    }
}

minecraft{
    version = minecraft_version + "-" + forge_version
    mappings = mcp_mappings
    runDir = "run"
}
version = project.major +"."+ project.minor +"."+  project.build+"-"+ project.releasetype + "-MC" +minecraft.version
group = "shadowmage"
archivesBaseName = "ancientwarfare"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    deobfCompile "cofh:RedstoneFlux:1.12-2.0.0.1:universal"
    //deobfCompile name: 'FTBLib', version: ftblib_version, ext: 'jar'
    //deobfCompile name: 'FTBUtilities', version: ftbutilities_version, ext: 'jar'
}

task updateVersion(type:Copy){
    ant.propertyfile(file: versionFile) {
        entry(key: 'build', type: 'int', operation: '+', value: 001)
    }
    from(sourceSets.main.java.srcDirs){
        exclude 'mcmod.info'
    }
    into 'build/sources/java'
    filter(ReplaceTokens, tokens: [VERSION: project.version])
}

compileJava.dependsOn updateVersion

processResources{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task expand_jar(type: Copy, dependsOn: reobf){
    from zipTree('build/libs/ancientwarfare-'+version+'.jar')
    into 'build/tmp/expanded'
}

task packFullJar(type: Jar, dependsOn: expand_jar){
    archiveName 'ancientwarfare-'+version+'-FULL.jar'
    from 'build/tmp/expanded'
    exclude 'net/shadowmage/ancientwarfare/modeler/', 'net/shadowmage/ancientwarfare/vehicle'
    manifest {
        attributes 'FMLAT': "ancientwarfare_at.cfg"
    }
    rename 'ancientwarfare_at.cfg', 'META-INF/ancientwarfare_at.cfg'
}

task packCoreJar(type: Jar, dependsOn: expand_jar){
    archiveName 'ancientwarfare-'+version+'-CORE.jar'
    from 'build/tmp/expanded'
    include 'net/shadowmage/ancientwarfare/api/', 'net/shadowmage/ancientwarfare/core/', 'assets/', 'AW_logo.png', 'mcmod.info', 'net/shadowmage/ancientwarfare/nei_plugin/'
}

task packAutomationJar(type: Jar, dependsOn: expand_jar){
    archiveName 'ancientwarfare-'+version+'-AUTOMATION.jar'
    from 'build/tmp/expanded'
    include 'net/shadowmage/ancientwarfare/automation/'
}

task packNpcJar(type: Jar, dependsOn: expand_jar){
    archiveName 'ancientwarfare-'+version+'-NPC.jar'
    from 'build/tmp/expanded'
    include 'net/shadowmage/ancientwarfare/npc/', 'ancientwarfare_at.cfg'
    manifest {
        attributes 'FMLAT': "ancientwarfare_at.cfg"
    }
    rename 'ancientwarfare_at.cfg', 'META-INF/ancientwarfare_at.cfg'
}

task packStructureJar(type: Jar, dependsOn: expand_jar){
    archiveName 'ancientwarfare-'+version+'-STRUCTURE.jar'
    from 'build/tmp/expanded'
    include 'net/shadowmage/ancientwarfare/structure/'
}

artifacts{
    archives packCoreJar
    archives packAutomationJar
    archives packNpcJar
    archives packStructureJar
    archives packFullJar
}





